buildscript {
    ext.kotlin_version = '1.1.1'
    ext.kovenant_version = '3.0.0'
    ext.spek_version = '1.1.2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/staging'
    }
    maven {
        url 'http://dl.bintray.com/jetbrains/spek'
    }

}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.junit.platform.gradle.plugin'

compileJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

group = 'io.github.vyo'
version = '1.2.0-SNAPSHOT'
description = 'Minimal bunyan-style logging for Kotlin and the JVM'

ext.sonatypeUsr = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
ext.sonatypePwd = project.hasProperty('sonatypePassword') ? sonatypePassword : ''

repositories {
    mavenCentral()
}

configurations {
    provided
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "nl.komponents.kovenant:kovenant:$kovenant_version"
    compile "nl.komponents.kovenant:kovenant-disruptor:$kovenant_version"
    testCompile "org.jetbrains.spek:spek-api:$spek_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testCompile 'com.winterbe:expekt:0.5.0'
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    from file('README.md')
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.archives


        beforeDeployment { MavenDeployment deployment ->
            signing.signPom(deployment)
        }


        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
            authentication(userName: sonatypeUsr, password: sonatypePwd)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: sonatypeUsr, password: sonatypePwd)
        }

        pom.whenConfigured { configgedPom ->
            configgedPom.dependencies.removeAll { dep ->
                dep.scope == "test"
            }

            configgedPom.dependencies = configgedPom.dependencies.sort { dep ->
                "$dep.scope:$dep.groupId:$dep.artifactId"
            }

            configgedPom.project {
                name project.name
                packaging('jar')
                description project.description
                url 'http://vyo.github.io/twig/'
                inceptionYear '2015'

                scm {
                    url 'https://github.com/vyo/twig'
                    connection 'scm:git:git@github.com/vyo/twig.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'vyo'
                        name 'Manuel Weidmann'
                        email 'weidmann.manuel@gmail.com'
                    }
                }

                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/vyo/twig/issues"
                }
            }
        }

    }
}
